# 如何从0搭建一个个人服务器？

###### 【Written By SoTWild】



如果你上网搜索这类的教程，你多半会找到以下答复：

> 学习网站前端（html、css、js）
>
> 购买阿里云服务器
>
> 使用树莓派
>
> 改装手机、电脑……

这些方案固然可行，但是大都免不了资金的问题，而且对于0基础人员来说软件上的操作也相当不友好，那么就没有办法了吗？

其实还有一个解决方案：使用**特殊硬件**，就好像**[这个网站](/)**一样，将网站搭建在**单片机**上。

接下来，我将详细列出搭建步骤：

------

### 1、选择硬件：

· 搜索 “**ESP32-CAM**” 获取最简单的硬件（20￥）

<img src="https://s1.328888.xyz/2022/10/01/Mztlw.jpg" style="zoom:50%;" />

<center>ESP32-CAM（其实连摄像头也不需要）</center>

· 找一个内存卡（TF卡），大小取决于你网站的规模、功能（最大32G）。（10￥）

<img src="http://img10.360buyimg.com/n1/g7/M03/08/1B/rBEHZlB7iQQIAAAAAAEsPR1ijWkAABuqwJE80gAASxV851.jpg" style="zoom: 33%;" />

<center>这个容量刚刚好</center>

### 2、软件：

###### 接下来的部分有点难度，但不难理解

· 安装**单片机的开发环境**，这里使用最简单的 **Arduino IDE** 。来到[官网](https://www.arduino.cc/en/software)选择 **Windows** 版本（或Mac OS，**取决于你电脑的操作系统**）下载。安装的过程比较简单，**该agree地方agree，该next地方next**，有一个要注意的地方就是其中一个页面中的 **Install USB driver** 一定**要勾选**，不然无法上传代码到开发板。

完成后应该是这样的：

<img src="https://www.technobyte.org/wp-content/uploads/2019/10/Arduino-IDE.png" style="zoom:30%;" />

· 下载开发板包：[Arduino 添加ESP32 ](https://cn.bing.com/search?q=Arduino+添加ESP32&form=ANNTH1&refig=3c8ff88049174484be226dfa7033e2b2&sp=6&qs=UT&pq=arduino+添加&sk=PRES1CT1SC4&sc=8-10&cvid=3c8ff88049174484be226dfa7033e2b2)

------

#### - 编写代码：

```c
#include <WiFi.h>
#include <WebServer.h>
#include <SD_MMC.h>

const char *ssid     = "********"; //你的WiFi名称
const char *password = "********"; //你的WiFi密码

WebServer server(80); //声明WebServer对象

void handleRoot() //回调函数
{
  File contentFile = SD_MMC.open("/index.html"); //从SD卡读取文件
  if (!contentFile)
  {
    Serial.println("读取文件失败");
    server.send(404, "text/plain", "访问失败");
    return;
  }
  String contentType = "text/html"; //Content-Type为HTML
  server.streamFile(contentFile, contentType); //向客户端发送文件
  contentFile.close();
}

void setup()
{
  Serial.begin(115200); //串口波特率115200
  Serial.println();

  WiFi.mode(WIFI_STA);
  WiFi.setSleep(false);
  WiFi.begin(ssid, password); //连接到无线网络
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }
  Serial.println("Connected");
  Serial.print("IP Address:");
  Serial.println(WiFi.localIP());

  if (!SD_MMC.begin()) //挂载SD卡
  {
    Serial.println("存储卡挂载失败");
    return;
  }

  server.on("/", handleRoot); //注册链接和回调函数

  server.begin(); //启动Web服务器
  Serial.println("Web服务器启动");
}

void loop()
{
  server.handleClient(); //处理来自客户端的请求
}
```

复制，粘贴到 IDE 中（替换IDE新建文件中自带的内容后，其中 5、6行对应改写），然后在 “**工具-开发板-ESP32 Arduino**” 中选择 **AI Thinker ESP32 CAM** ，再在 “**工具-端口**“ 中选择**开发板所在的串行端口**，最后用 **Micro USB** 数据线连接电脑和开发板，点击**上传（Ctrl + U）**。一分钟后，看到 ”上传成功。“ 的字样，再按 **Ctrl + Shift + M 打开串口监视器**，点击开发板上的 “**RST**” 按键。

可以看见输出：

> IP Address:192.168.x.xxx
>
> Web服务器启动

其中 ”**192.168.x.xxx**” 为设备的 **IP 地址**，当你的电脑和开发板**处于同一局域网**时，在浏览器里输入该 IP 地址后，你就可以看见“服务器”做出了相应（此处应为404）。

如果出现 “**存储卡挂载失败**” 那么将TF卡进行**格式化**，格式化为 **FAT32** 。再试，还不行就换另一种卡或大小。

------

#### - SD卡文件：

来到SD卡**根目录**，新建一个名为 “**index**” 的**文本文件**，写入：

```html
<!DOCTYPE html>

<head>
    <title>SD WebServer Test</title>
</head>

<body>
    <p style="background-color:gray">这是来自SD卡的html文件</p>
</body>

</html>

```

然后保存，将**文件后缀名改为 “html”**，再插回开发板的卡槽内。**重启**开发板，访问地址，就会出现：

> 这是来自SD卡的html文件

这样一来，就完成了**一半**了。

你可以使用各种**有导出为html功能的编辑软件**，写下你要的文字后导出为 “i**ndex.html**” **替换之前的文件**，就可以实现网站内容的更换。

但是这个网站目前**只能内网访问**，一旦访问者脱离局域网，就无法访问，所以还需要 “**内网穿透**”。

------

### 3、内网穿透：

有很多内网穿透服务，我推荐[OpenFrp内网穿透 | 官方网站](https://www.openfrp.net/)，注册、登录后新建隧道，填入设备的IP地址，然后启用隧道，就大功告成了。现在即使身处外网，也可以顺利访问。

**最后将网站进行备案。**

~~这篇科普好像应该归为教程……~~



### 其他：

关于 ESP32 ：

**ESP32** 芯片是由我国的**乐鑫**公司继 ESP8266 芯片后推出的又一款集成 WiFi 功能的微控制器。它拥有比 ESP8266 **更加强大**的性能，可以用来开发**更加复杂**的应用。ESP32 芯片或模组具有下列特点：

> - **处理器：Tensilica LX6 双核处理器（一核处理高速连接；一核独立应用开发）**
> - **主频：32 位双核处理器，CPU 正常工作速度为 80 MHz，最高可达 240 MHz**
> - **SRAM：520KB，最大支持 8 MB 片外 SPI SRAM**
> - **Flash：最大支持 16 MB 片外 SPI Flash**
> - **WiFi 协议：支持 802.11 b/g/n/d/e/i/k/r 等协议，速度高达150 Mbps**
> - **频率范围：2.4~2.5 GHz**
> - **蓝牙协议：支持蓝牙 v4.2 完整标准，包含传统蓝牙 (BR/EDR) 和低功耗蓝牙 (BLE)**
> - **外设接口：GPIO、ADC、DAC、SPI、I²C、I²S、UART 等**

ESP32 芯片主要具有**高性能、低功耗、高度集成、体积小、性能稳定**等优点。
